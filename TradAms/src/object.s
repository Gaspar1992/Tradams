.area _DATA

TradamsLife:
	.db #0x00, #0x01, #0x00, #0x00
	.db #0x00, #0x13, #0x08, #0x00
	.db #0x00, #0x37, #0x8c, #0x00
	.db #0x00, #0x5f, #0x4e, #0x00
	.db #0x01, #0xaf, #0xef, #0x00
	.db #0x13, #0x5f, #0xdf, #0x08
	.db #0x27, #0xbd, #0xef, #0x8c
	.db #0x7f, #0x78, #0xf9, #0x4e
	.db #0x27, #0xfc, #0xf1, #0xef
	.db #0x13, #0xfe, #0xf3, #0xce
	.db #0x01, #0xfc, #0xf1, #0x8c
	.db #0x00, #0x7c, #0xf8, #0x08
	.db #0x00, #0x37, #0xed, #0x00
	.db #0x00, #0x13, #0xce, #0x00
	.db #0x00, #0x01, #0x8c, #0x00
	.db #0x00, #0x00, #0x08, #0x00

Tradams3_5:
	.db #0x00, #0x01, #0x00, #0x00
	.db #0x00, #0x12, #0x08, #0x00
	.db #0x00, #0x34, #0x84, #0x00
	.db #0x00, #0x1a, #0x4e, #0x00
	.db #0x01, #0x05, #0xef, #0x00
	.db #0x02, #0x1b, #0xce, #0x08
	.db #0x05, #0x37, #0xef, #0x04
	.db #0x08, #0x6f, #0x3f, #0x4e
	.db #0x05, #0xcf, #0x17, #0xef
	.db #0x13, #0xce, #0x1b, #0xce
	.db #0x01, #0xcf, #0x17, #0x8c
	.db #0x00, #0x6f, #0x3f, #0x08
	.db #0x00, #0x37, #0xef, #0x00
	.db #0x00, #0x13, #0xce, #0x00
	.db #0x00, #0x01, #0x8c, #0x00
	.db #0x00, #0x00, #0x08, #0x00

Tradams256Kbs:
	.db #0x0f, #0x0f, #0x0f, #0x0f
	.db #0x0c, #0x0c, #0x03, #0x01
	.db #0x0b, #0x05, #0x0e, #0x0f
	.db #0x0f, #0x06, #0x06, #0x03
	.db #0x0e, #0x0f, #0x0a, #0x0d
	.db #0x0d, #0x0f, #0x0a, #0x0d
	.db #0x08, #0x04, #0x07, #0x03
	.db #0x0f, #0x0f, #0x0f, #0x0f
	.db #0x0c, #0x09, #0x08, #0x07
	.db #0x0c, #0x01, #0x09, #0x0b
	.db #0x0c, #0x03, #0x09, #0x0b
	.db #0x0c, #0x07, #0x08, #0x07
	.db #0x0c, #0x03, #0x09, #0x0b
	.db #0x0c, #0x01, #0x09, #0x0b
	.db #0x0c, #0x09, #0x08, #0x07
	.db #0x0f, #0x0f, #0x0f, #0x0f

CDisk:
    .db #0x00, #0x07, #0x0e, #0x00
	.db #0x01, #0x7f, #0xef, #0x08
	.db #0x13, #0xff, #0xf7, #0x8c
	.db #0x37, #0xfb, #0xff, #0xce
	.db #0x37, #0xff, #0xff, #0xce
	.db #0x7f, #0xcf, #0x3f, #0xef
	.db #0x7c, #0x8c, #0x13, #0xe7
	.db #0x7f, #0x8c, #0x13, #0xef
	.db #0x7f, #0x8c, #0x13, #0xef
	.db #0x7d, #0x8c, #0x13, #0xeb
	.db #0x7f, #0xcf, #0x3f, #0xef
	.db #0x37, #0xff, #0xff, #0xce
	.db #0x37, #0xf7, #0xf7, #0xce
	.db #0x13, #0xff, #0xff, #0x8c
	.db #0x01, #0x7f, #0xef, #0x08
	.db #0x00, #0x07, #0x0e, #0x00

usb:
    .db #0x00, #0x00, #0x00, #0x00
	.db #0x00, #0x88, #0x00, #0x00
	.db #0x11, #0x44, #0x00, #0x00
	.db #0x22, #0xaa, #0x00, #0x00
	.db #0x55, #0x55, #0x00, #0x00
	.db #0x44, #0xaa, #0x88, #0x00
	.db #0x22, #0x55, #0x44, #0x00
	.db #0x11, #0x44, #0xaa, #0x00
	.db #0x00, #0xaa, #0x77, #0x00
	.db #0x00, #0x55, #0x46, #0x00
	.db #0x00, #0x22, #0xe9, #0x00
	.db #0x00, #0x11, #0x78, #0x08
	.db #0x00, #0x00, #0x25, #0x00
	.db #0x00, #0x00, #0x02, #0x00
	.db #0x00, #0x00, #0x00, #0x00
	.db #0x00, #0x00, #0x00, #0x00

jostick:
    .db #0x00, #0x00, #0x00, #0x00
	.db #0x00, #0x03, #0x0c, #0x00
	.db #0x00, #0x34, #0xc2, #0x00
	.db #0x00, #0x34, #0xc2, #0x00
	.db #0x00, #0x34, #0xc2, #0x00
	.db #0x00, #0x13, #0x8c, #0x00
	.db #0x00, #0x11, #0x88, #0x00
	.db #0x00, #0x11, #0x88, #0x00
	.db #0x00, #0xff, #0xff, #0x00
	.db #0x11, #0x11, #0x88, #0x88
	.db #0x11, #0x13, #0x8c, #0x88
	.db #0x22, #0x04, #0x02, #0x44
	.db #0x22, #0x03, #0x0c, #0x44
	.db #0x33, #0xff, #0xff, #0xcc
	.db #0x00, #0x00, #0x00, #0x00
	.db #0x00, #0x00, #0x00, #0x00

glitch:
    .db #0x00, #0x00, #0x00, #0x00
	.db #0x12, #0x2d, #0x87, #0x84
	.db #0x12, #0x2d, #0x87, #0x84
	.db #0x12, #0x2d, #0x87, #0x84
	.db #0x12, #0x2d, #0x87, #0x84
	.db #0x12, #0x2d, #0x87, #0x84
	.db #0x12, #0x2d, #0x87, #0x84
	.db #0x12, #0x2d, #0x87, #0x84
	.db #0x12, #0x2d, #0x87, #0x84
	.db #0x12, #0x2d, #0x87, #0x84
	.db #0x12, #0x2d, #0x87, #0x84
	.db #0x12, #0x2d, #0x87, #0x84
	.db #0x12, #0x2d, #0x87, #0x84
	.db #0x12, #0x2d, #0x87, #0x84
	.db #0x12, #0x2d, #0x87, #0x84
	.db #0x00, #0x00, #0x00, #0x00

symbol_a:
    .db #0x00, #0x00, #0x10, #0x00
	.db #0x30, #0xf0, #0xf0, #0x80
	.db #0x00, #0x00, #0x10, #0x00
	.db #0x11, #0x22, #0x00, #0x00
	.db #0x33, #0xff, #0x00, #0x00
	.db #0x11, #0x22, #0x11, #0x00
	.db #0x00, #0x00, #0x11, #0x88
	.db #0x00, #0xff, #0xff, #0xcc
	.db #0x00, #0x00, #0x11, #0x88
	.db #0x00, #0x00, #0x11, #0x00
	.db #0x00, #0x09, #0x00, #0x00
	.db #0x01, #0x0f, #0x08, #0x00
	.db #0x00, #0x09, #0x00, #0x00
	.db #0x00, #0x00, #0x10, #0x00
	.db #0x30, #0xf0, #0xf0, #0x80
	.db #0x00, #0x00, #0x10, #0x00


;;===============================================================================
;; DEFINED CONSTANTS
;;===============================================================================

.macro defineObjectAction name , id , sprite , size , affects , objective , value  
    name'_object_action::
        name'_id:           .db id          ;; Entity ID
        name'_sprite:       .dw sprite      ;; Entity Sprite
        name'_size:         .dw size        ;; Size of Entity
        name'_affects:      .db affects     ;; Entity Player Affects (Skills , Bag , ...)
        name'_objective:    .db objective   ;; Entity Objective
        name'_value:        .db value       ;; Entity Value
.endm

.macro defineObjectValues name , id
    name'_object :: .db id          ;; Object ID
.endm
;;===============================================================================
;;  DEFINE OBJECT
;; defineObjectAction name , id , sprite , size , affects , objective , value 
;; defineObjectValues name , id
;;===============================================================================
all_objects::
defineObjectValues a , 1    ;; 1
defineObjectValues b , 2    ;; 2
defineObjectValues c , 3    ;; 3
defineObjectValues d , 4    ;; 4
defineObjectValues e , 5    ;; 5
defineObjectValues f , 6    ;; 6
defineObjectValues g , 7    ;; 7
defineObjectValues h , 8    ;; 8
defineObjectValues i , 9    ;; 9
defineObjectValues j , 10   ;; 10


;;==============================================================================================
;; SET OBJECTS INTO GAME
;;==============================================================================================
;; ## OBJECT COMPOSITION ##
;; ########################
;; defineObjectAction name , id , sprite , size , affects , objective , value 
;; id = Object ID (UNIQUE)
;; sprite = Pointer to Sprite Object
;; size   = Sprite size
;; affects =>   0 = PLAYER SKILLS   || 1 = PLAYER BAG
;;
;; objective =>  PLAYER SKILLS      ||  PLAYER BAG
;;           0 | Movement Speed X   ||  Player Bits
;;           1 | Movement Speed Y   ||  Player Keys
;;           2 | Actual Player Life ||  Player Bombs
;;           3 | Max Player Life
;;           4 | Attack Damage
;;           5 | Attack Fire Rate
;;           6 | Attack Speed
;;           7 | Attack Range
;; value = how many increment or decrement player values
;;==============================================================================================
;; IMPORTANT! UPDATE number_of_defined_objects_id with last ID OBJECT Value
;;==============================================================================================

defined_objects_id::
;; SKILLS OBJECTS
defineObjectAction half_life , 1 , #player_life_2 , #0x0802 , 0 , 2 , 1
defineObjectAction life , 2 , #player_life_3 , #0x0802 , 0 , 2 , 2
;; BAG OBJECTS
defineObjectAction bits , 3 , #bit_coin , #0x0803 , 1 , 0 , 1
defineObjectAction keys , 4 , #TradamsKeys , #0x0802 , 1 , 1 , 1
defineObjectAction mina , 5 , #TradamsMina_0 , #0x0802 , 1 , 2 , 1
;; SKILLS OBJECTS_2
defineObjectAction more_life , 6 , #TradamsLife , #0x1004 , 0 , 3 , 2
defineObjectAction more_dmg  , 7 , #TradamsDMG , #0x0802 , 0 , 4 , 1
defineObjectAction more_frrt , 8 , #TradamsFireRate , #0x0802 , 0 , 5 ,-3
defineObjectAction attack_sp , 9 , #Tradams3_5 , #0x1004 , 0 , 6 , 1
defineObjectAction heal_all , 10 , #Tradams256Kbs , #0x1004 , 0 , 2 , 20
defineObjectAction cdisk    , 11 , #CDisk         , #0x1004 , 0 , 3 , 4
defineObjectAction usb      , 12 , #usb           , #0x1004 , 0 , 7 , 2
defineObjectAction jostick_a  , 13 , #jostick       , #0x1004 , 0 , 1 , 2
defineObjectAction jostick_b  , 14 , #jostick       , #0x1004 , 0 , 0 , 1
defineObjectAction glitch   , 15 , #glitch        , #0x1004 , 1 , 1 , 10
defineObjectAction symbol_a , 16 , #symbol_a      , #0x1004 , 0 , 6 , 2

number_of_defined_objects_id = #16
number_of_parameters = #8


.globl all_position_objects
.globl number_of_position_parameters
;;Player
.globl player_add_skills_values_asm
.globl player_add_consumables_values_asm
.globl player_bag
.globl player_skills
.globl player_position
;; Objects Sprites
.globl player_life_2
.globl player_life_3
.globl bit_coin
.globl TradamsKeys
.globl TradamsMina_0
.globl TradamsDMG
.globl TradamsFireRate
.globl hud_changes

;; Functions
.globl collision_entities_asm
.globl check_sprite_draw
.globl draw_entity_asm
.globl move_entity_asm
.globl simple_loop_inc_hl
.globl clean_last_position_entity

.area _CODE 

number_of_objects = #10
;;=============================================================
;; FUNCTION GET_OBJECT_VOID
;; Output Parameters: HL = Pointer to Objects Array
;;=============================================================
get_object_void:
    ld hl , #all_objects        ;; HL = Pointer to All Objects
    ld  c , #0                  ;;  C = 0
    ld  b , #number_of_objects  ;; B = Number of Objects
_loop_object_void:
    ld a , (hl)                 ;; A = Value in Array
    or a                        ;; Active Flags
    ret z                       ;; Return if A = 0
    inc c                       ;; Increment C Value
    inc hl                      ;; Next Object Array position
    djnz _loop_object_void
ret
;;=============================================================
;; FUNCTION SET OBJECT SCREEN ASM
;; 
;;  Input Parameters :  D = id
;;                     BC = Screen Position (B = Y , C = X)
;;=============================================================
set_object_screen_asm::
    push bc                 ;; Save BC Values (Screen Position)
    call get_object_void    ;; Get Space to Fill
    ld  e , c               ;; E = Array Position(C)
    pop bc                  ;; Recover BC
    ret nz                  ;; IF NoT exist Space
    push bc                 ;; Save BC Values (Screen Position)
    ld (hl) , d             ;; (HL) Objects Array = Id Object
    call get_object_by_id   ;; Get Object by ID
    push hl                 ;; Save HL
    pop  ix                 ;; Recover HL into IX
    call get_object_position_by_array_pos ;; Get Object by Array Pos
    push hl                 ;; Save HL
    pop  iy                 ;; Recover HL into IY
    pop  bc                 ;; Recover BC
    ld a , 3(ix)            ;; A = Width Object
    ld 1(iy) , a            ;; IY = Width(A)
    ld a , 4(ix)            ;; A = Height Object
    ld 4(iy) , a            ;; IY = Height Object
    ld 0(iy) , c            ;; IY = X Pos
    dec c                   ;; Decrement C
    ld 2(iy) , c            ;; IY = Last X
    ld 3(iy) , b            ;; IY = Y Pos
    ld 5(iy) , b            ;; IY = Last Y
ret

;;=============================================================
;; FUNCTION GET OBJECT BY ID
;; 
;;  Input Parameters :  D = Id
;; Output Parameters : HL = Action Object
;;=============================================================
get_object_by_id:
    ld hl , #defined_objects_id             ;; HL = Pointer to Defined Action Object
    ld b , #number_of_defined_objects_id
    ld c , #number_of_parameters            ;; C = Number of Parameters
_getter_object_by_id_loop:
    ld a , (hl)                             ;;  A = (HL)
    cp d                                    ;; COMPARE WITH D
    ret z                                   ;; IF A == 0 RETURN
    ld a , c                                ;; A = C
    push bc                                 ;; Save BC
    call simple_loop_inc_hl
    pop bc                                  ;; Recover BC
    djnz _getter_object_by_id_loop
ret

;;=============================================================
;; FUNCTION GET OBJECT POSITION BY ARRAY POSITION
;; 
;;  Input Parameters :  E = Array Position
;; Output Parameters : HL = Pointer Object Position
;;=============================================================
get_object_position_by_array_pos:
    ld hl , #all_position_objects           ;; Pointer to Position Objects
    ld a , e                                ;; A = E
    or a                                    ;; Active Flags
    ret z                                   ;; IF A == 0 RETURN
    ld b , e                                ;; B = E
    ld de , #number_of_position_parameters  ;; Pointer to NOPP
    ld  a , (de)                            ;; A = NOPP
    ld  c , a                               ;; C = A
    xor a                                   ;; XOR A
_getter_object_by_array_loop:
    add a , c                               ;; A = A + C
    djnz _getter_object_by_array_loop
    call simple_loop_inc_hl
ret

;;=============================================================
;; FUNCTION RESET_OBJECTS_ASM
;;=============================================================
reset_objects_asm::
    ld hl , #all_objects        ;; HL = Pointer to Array Objects
    ld  b , #number_of_objects  ;;  B = Number Of Objects
    ld  c , #0                  ;; Array Position
_loop_reset_objects::
    ld (hl) , #0                ;; HL = 0
    inc hl                      ;; Next Object (HL)
    djnz _loop_reset_objects
ret
;;=============================================================
;; FUNCTION USE OBJECT ASM
;; 
;;  Input Parameters :  D = ID
;;                      C = Array Pos
;;                     HL = Object Position
;;=============================================================
use_object_asm::
    ld e , c                        ;; E = Array Pos
    push de                         ;; Save DE
    call clean_last_position_entity ;; Clean Entity
    pop  de                         ;; Recover DE
    ld  a , e                       ;; A = Array Pos 
    ld hl , #all_objects            ;; HL = All Objects Pointer
    call simple_loop_inc_hl         ;; HL Loop
    ld (hl) , #0                    ;; HL = 0
    call get_object_by_id           ;; Get Object By ID
    push hl                         ;; Save HL
    pop ix                          ;; Recover HL into IX
    ld a , 5(ix)                    ;; Player Part Objective
    or a                            ;; Active Flags
    ld a , 6(ix)                    ;; A = Object Increment
    ld e , 7(ix)                    ;; E = Value 
    jr nz  , _player_bag
    call z , player_add_skills_values_asm
    ret
_player_bag:
    call player_add_consumables_values_asm
ret

;;=============================================================
;; FUNCTION DRAW_OBJECTS_ASM
;;=============================================================
draw_objects_asm::
    ld hl , #all_objects        ;; HL = Pointer to Array Objects
    ld  b , #number_of_objects  ;; B = Number of Objects
    ld  c , #0                  ;; Array Pos
_loop_draw_objects:
    ld a , (hl)                 ;; A = Id Object
    or a                        ;; Active Flags
    call nz , draw_single_object;; Draw Single Object
    inc hl                      ;; Next HL Position
    inc c                       ;; Increment Array Pos
    djnz _loop_draw_objects
ret
;;=============================================================
;; FUNCTION DRAW_SINGLE_OBJECT
;; Input Parameters : A = ID
;;                    C = Array Pos
;;=============================================================
draw_single_object:
    push hl                                     ;; Save HL Values
    push bc                                     ;; Save BC Values
    ld d , a                                    ;; D = A(ID)
    call get_object_by_id                       ;; Get Object By ID
    push hl                                     ;; Save HL
    pop ix                                      ;; Recover HL into IX
    pop bc                                      ;; Recover BC
    push bc                                     ;; Save BC
    ld e , c                                    ;; E = Array Pos(C)
    call get_object_position_by_array_pos       ;; Object Array Pos
    ld e , 1(ix)                                ;; First Sprite Part
    ld d , 2(ix)                                ;; Second Sprite Part
    push hl                                     ;; Save HL
    call check_sprite_draw                      ;; CHECK DRAW
    pop hl                                      ;; Recover HL
    call nz , draw_entity_asm                   ;; Draw Entity
    pop  bc                                     ;; Recover BC Values
    pop  hl                                     ;; Recover HL Values
ret
;;=============================================================
;; FUNCTION OBJECTS_COLLISION_ASM
;;=============================================================
objects_collision_asm::
    ld hl , #all_objects        ;; HL = Pointer to Array Objects
    ld  b , #number_of_objects  ;; B = Number of Objects
    ld  c , #0                  ;; Array Pos
_loop_object_collision:
    ld a , (hl)                 ;; A = HL
    or a                        ;; Active Flags
    call nz , collision_single_object
    inc c                       ;; Array Pos
    inc hl                      ;; HL Pos
    djnz _loop_object_collision
ret
;;=============================================================
;; FUNCTION COLLISION_SINGLE_OBJECT
;; Input Parameters : A = ID
;;                    C = Array Pos
;;=============================================================
collision_single_object:
    push hl                                     ;; Save HL Values
    push bc                                     ;; Save BC Values
    push af                                     ;; Save AF Values
    ld e , c                                    ;; E = Array Pos(C)
    call get_object_position_by_array_pos       ;; Get Object Pos
    push hl                                     ;; Save HL
    pop ix                                      ;; Recover HL Values into IX
    ld iy , #player_position                    ;; IY = Player Position
    pop de                                      ;; Recover Values AF into DE
    call collision_entities_asm                 ;; Collision A != 0
    or a                                        ;; Active Flags
    pop bc                                      ;; Recover BC Values
    push bc                                     ;; Save BC
    call nz , use_object_asm                    ;; USE Object
    pop  bc                                     ;; Recover BC Values
    pop  hl                                     ;; Recover HL Values
ret